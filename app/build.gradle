buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.7'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def versionMajor = 1
def versionMinor = 9
def versionPatch = 9
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    project.ext {
        CABINET_FABRIC_KEY = project.hasProperty("CABINET_FABRIC_KEY") ? project.CABINET_FABRIC_KEY : "";
        CABINET_BILLING_KEY = project.hasProperty("CABINET_BILLING_KEY") ? project.CABINET_BILLING_KEY : "\"\"";
    }

    defaultConfig {
        applicationId "us.fetch321.cabinet"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        buildConfigField "String", "CABINET_BILLING_KEY", project.ext.CABINET_BILLING_KEY
        manifestPlaceholders = [fabric_key: project.ext.CABINET_FABRIC_KEY]
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dexOptions {
        dexInProcess true
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':RootTools')

    compile 'com.afollestad.material-dialogs:core:0.8.6.2'
    compile 'com.afollestad.material-dialogs:commons:0.8.6.2'
    compile 'com.afollestad:material-cab:0.1.12'

    compile 'com.github.bumptech.glide:glide:3.6.1'
    compile 'com.wnafee:vector-compat:1.0.5'
    compile 'com.github.clans:fab:1.6.4'

    compile 'com.anjlab.android.iab.v3:library:1.0.32@aar'

    compile 'org.tukaani:xz:1.5'
    compile 'org.apache.commons:commons-compress:1.12@jar'

    compile 'com.android.support:recyclerview-v7:24.1.1'
    compile 'com.android.support:support-v13:24.1.1'
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') {
        transitive = true;
    }

    compile 'com.github.pluscubed:recycler-fast-scroll:0.3.2'
}

